pre {
	"Begin Transformation...".println();
	var aeToDiscard = new Sequence;
}

post {
	"Transformation Complete".println();
	for(ae in aeToDiscard)
	{
		delete ae;
	}
}


rule Module2ArgumentPackage
	transform m:GSN!Module
	to ap: SACM!ArgumentPackage {
	ap.gid = m.gid;
	ap.argumentAsset = m.argumentAsset.equivalent();
}

rule Goal2Claim 
	transform g: GSN!Goal
	to c: SACM!Claim {
	c.gid = g.gid;
	c.content = g.content;
	
	//normal goal
	if(g.undeveloped = false and g.toBeSupportedByContract = false and g.uninstantiated = false)
	{
		c.assumed = false;
		c.isAbstract = false;
	}
	//Undeveloped Goal
	else if(g.undeveloped = true and g.toBeSupportedByContract = false and g.uninstantiated = false)
	{
		c.assumed = false;
		c.isAbstract = true;
	}
	//Uninstantiated Goal
	else if(g.undeveloped = false and g.toBeSupportedByContract = false and g.uninstantiated = true)
	{
		c.assumed = true;
		c.isAbstract = true;
	}
	//ToBeSupportedByContract Goal
	else if(g.undeveloped = false and g.toBeSupportedByContract = true and g.uninstantiated = false)
	{
		c.assumed = false;
		c.isAbstract = false;
		c.toBeSupported = true;
	}
	else if(g.undeveloped = false and g.toBeSupportedByContract = true and g.uninstantiated = true)
	{
		c.assumed = true;
		c.isAbstract = true;
		c.toBeSupported = true;
	}
}

rule Solution2ArtefactElementCitation
	transform s: GSN!Solution
	to aec: SACM!ArtefactElementCitation {
	aec.gid = s.gid;
	aec.content = s.content;
	if(s.uninstantiated = true)
	{
		aec.isAbstract = true;
	}
	else
	{
		//to be discussed
		aec.isAbstract = false;
	}
}

rule Strategy2ArgumentReasoning 
	transform s: GSN!Strategy
	to ai: SACM!AssertedInference, ae: SACM!AssertedEvidence, ar: SACM!ArgumentReasoning {
	
	var upperLevel = getSupportedByForStrategyAsTarget(s);
	var lowerLevel = getSupportedByForStrategyAsSource(s);

	ar.gid = s.gid;
	ar.content = s.content;
	
	var supportedByConnectingSolutions: Collection = lowerLevel.select(sb|sb.target.exists(t|t.isTypeOf(GSN!Solution)));
	if(supportedByConnectingSolutions.isEmpty())
	{
		aeToDiscard.add(ae);
	}
	else
	{
		ae.source.addAll(supportedByConnectingSolutions.target.flatten().asSet().equivalent());
		ae.target.addAll(upperLevel.source.flatten().asSet().equivalent());
		ae.reasoning = ar;
	}
	ai.source.addAll(lowerLevel.target.flatten().asSet().equivalent());
	ai.target.addAll(upperLevel.source.flatten().asSet().equivalent());
	ai.reasoning = ar;
}

rule Context2ArtefactElementCitation
	transform c: GSN!Context
	to aec: SACM!ArtefactElementCitation {
	guard: c.refersToExternalMaterial = true
	aec.gid = c.gid;
	aec.content = c.content;
	if(c.uninstantiated = true)
	{
		aec.isAbstract = true;
	}
	else 
	{
		aec.isAbstract = false;
	}
}

rule Context2Claim 
	transform context: GSN!Context
	to claim: SACM!Claim {
	guard: context.refersToExternalMaterial = false or context.refersToExternalMaterial.isUndefined()
	claim.isAbstract = false;
	claim.assumed = false;
	claim.gid = context.gid;
	claim.content = context.content;
	var taggedValue = new SACM!TaggedValue;
	taggedValue.sKey = "isBasic";
	claim.taggedValue.add(taggedValue);
	//discuss
}

rule Justification2Claim
	transform j: GSN!Justification
	to c: SACM!Claim {
	c.isAbstract = false;
	c.assumed = false;
	c.gid = j.gid;
	c.content = j.content;
	//justification should be a basic claim?
}

rule Assumption2Claim
	transform j: GSN!Assumption
	to c: SACM!Claim {
	c.isAbstract = false;
	c.assumed = true;
	c.gid = j.gid;
	c.content = j.content;
}

rule SupportedBy2AssertedEvidence
	transform sb: GSN!SupportedBy
	to ae: SACM!AssertedEvidence {
	guard : sb.source.forAll(s|s.isTypeOf(GSN!Goal)) and sb.target.forAll(t|t.isTypeOf(GSN!Solution))
	ae.source.addAll(sb.target.equivalent());
	ae.target.addAll(sb.source.equivalent());
}

rule SupportedBy2AssertedInference
	transform sb: GSN!SupportedBy
	to ai: SACM!AssertedInference {
	guard : sb.source.forAll(s|s.isTypeOf(Goal)) and sb.target.forAll(t|t.isTypeOf(Goal))
	ai.source.addAll(sb.target.equivalent());
	ai.target.addAll(sb.source.equivalent());	
}

rule InContextOf2AssertedContext
	transform ico: GSN!InContextOf
	to ac: SACM!AssertedContext {
	ac.source.addAll(ico.target.equivalent());
	ac.target.addAll(ico.source.equivalent());
}

operation getSupportedByForStrategyAsSource(strategy: GSN!Strategy)
{
	var allSupportedBy = getAllSupportedBy();
	
	return allSupportedBy.select(sb|sb.source.includes(strategy));
}

operation getSupportedByForStrategyAsTarget(strategy: GSN!Strategy)
{
	var allSupportedBy = getAllSupportedBy();
	return allSupportedBy.select(sb|sb.target.includes(strategy));
}

@cached
operation getAllSupportedBy()
{
	return GSN!SupportedBy.allInstances();
}

operation createTaggedValueWithSimpleKey(key: String)
{
	var taggedValue = new SACM!TaggedValue;
	taggedValue.sKey = key;
}